
# EIP 181: ENS 支持反向解析以太坊地址

| 作者 | 状态 | 类型 | 分类 | 创建时间 |
| --- | --- | --- | --- | --- | --- |
| Nick Johnson <arachnid@notdot.net> | Final | Standards Track | ERC | 2016-12-01 |

## 简要说明
这条EIP主要定义了一个顶级域名，注册器，以及使用ENS来反向解析以太坊地址的解析接口。这允许使用一个可读性高的名称来关联一个以太坊区块链的地址。解析器可以确定反向记录是由以太坊地址的所有者发布的。

## 动机
虽然域名服务大多数情况用于正向解析 - 从方便人类阅读的标识符到方便机器表示的标识符 - 但在许多用例（或场景）中，反向解析也很有用：
* 允许用户监控账户的应用就会因为显示的是账户名而不是其地址而得到好处，即使它最初是通过地址添加的。
* 把诸如描述性信息之类元数据添加到地址上以允许检索这个信息，而不用管这个地址最初是如何被发现的。
* （正向解析时）任何人都可以为地址配置一个名称，而不用关心地址的所有者。相比之下，反向记录允许地址的所有者对这个地址声明所有权。

## 规范
反向的ENS记录和常规的记录一样都存储在ENS层次结构中的一个保留域 `addr.reverse` 中.若要为一个给定账户的反向记录生成ENS名称，需将此账户转换为小写的十六进制表示，并在它之后添加 `addr.reverse` 。比如，ENS 注册的地址是 `0x112234455c3a32fd11230c42e7bccd4a84e02010` 那么它的反向记录则存储在 `112234455c3a32fd11230c42e7bccd4a84e02010.addr.reverse` 。

注意，这意味若想要在合约中动态的反向解析地址，那么就需要在合约中执行十六进制的编码操作。

## 注册器
`addr.reverse` 域的所有者就是一个注册器，它允许调用者获取他们自己地址中的反向记录的所有权。它提供了如下几种方法：

### function claim(address owner) returns (bytes32 node)
当账户 `x` 调用这个函数的时候，它指示 ENS 注册表转移一个名为 `hex(x) + 'addr.reverse'` 的所有权到参数提供的地址，并返回这个ENS记录转移的哈希值。
允许调用者指定自身以外的地址（作为所有者），这对于一些合约很有用，比如它需要把反向ENS记录委派给其创建者， 这时只需要他们的构造函数里添加一段小小的代码：

```
reverseRegistrar.claim(msg.sender)
```

### function claimWithResolver(address owner, address resolver) returns (bytes32 node)
当账户 `x` 调用这个函数的时候，它指示 ENS 注册表为名为 `hex(x) + '.addr.reverse'` （的地址）设置一个指定的解析器，然后转移它的所有权到参数提供的地址中，并返回这个 ENS 记录转移的哈希值。

这个方法和 直接调用 claim 相比， 可以在更少的的交易里设置自定义解析器和所有者。

### function setName(string name) returns (bytes32 node)
当账户 `x` 调用这个函数的时候，就表示对名为 `hex(x) + '.addr.reverse'` （的地址）设置一个默认的解析器，并将该名称上的名称记录设置为指定的名称。这个方法对于用户需要在单个交易上设置一个简单的反向记录比较有用。

## 解析器接口
定义了一个新的解析器接口，包括下面的方法:

```
function name(bytes32 node) constant returns (string);
```

实现此接口的解析器必须为指定的节点返回一个合法的ENS名称，若指定的节点没有定义一个名称，则返回一个空字符串。

这个接口的接口 `ID` 是 `0x691f3431`。

未来的 EIPs 可能会指定更多适用于反向 ENS 记录的记录类型

# 附录1：注册器的实现
如下的注册器使用 Solidity 编写，实现了上面列出的规范。

```
pragma solidity ^0.4.10;

import "./AbstractENS.sol";

contract Resolver {
    function setName(bytes32 node, string name) public;
}

/**
 * @dev 提供反向记录解析器的一个默认实现，此解析器仅允许所有者更新它
 */
contract DefaultReverseResolver is Resolver {
    AbstractENS public ens;
    mapping(bytes32=>string) public name;

    /**
     * @dev 构造函数
     * @param ensAddr, ENS 注册表的地址
     */
    function DefaultReverseResolver(AbstractENS ensAddr) {
        ens = ensAddr;
    }

    /**.
     * @dev 只允许反向注册器调用 
     * @param node的需要有权限
     */
    modifier owner_only(bytes32 node) {
        require(msg.sender == ens.owner(node));
        _;
    }

    /**
     * @dev 设置节点的名称
     * @param node 要更新的节点
     * @param _name 要设置的名字
     */
    function setName(bytes32 node, string _name) public owner_only(node) {
        name[node] = _name;
    }
}

contract ReverseRegistrar {
    // namehash('addr.reverse')
    bytes32 constant ADDR_REVERSE_NODE = 0x91d1777781884d03a6757a803996e38de2a42967fb37eeaca72729271025a9e2;

    AbstractENS public ens;
    Resolver public defaultResolver;

    /**
     * @dev 构造函数
     * @param ensAddr，ENS注册表的地址
     * @param resolverAddr，默认反向解析器的地址
     */
    function ReverseRegistrar(AbstractENS ensAddr, Resolver resolverAddr) {
        ens = ensAddr;
        defaultResolver = resolverAddr;
    }

    /**
     * @dev  转移调用者账户反向ENS记录的所有权
     * @param owner, 用来设置ENS中反向记录所有者的地址
     * @return ENS 反向记录节点的哈希
     */
    function claim(address owner) returns (bytes32 node) {
        return claimWithResolver(owner, 0);
    }

    /**
     * @dev 转移调用者账户反向ENS记录的所有权
     * @param owner, 用来设置ENS中反向记录所有者的地址
     * @param resolver, 设置的解析器的地址；0 则不变
     * @return ENS 反向记录节点的哈希
     */
    function claimWithResolver(address owner, address resolver) returns (bytes32 node) {
        var label = sha3HexAddress(msg.sender);
        node = sha3(ADDR_REVERSE_NODE, label);
        var currentOwner = ens.owner(node);

        // 若满足条件则更新解析器
        if(resolver != 0 && resolver != ens.resolver(node)) {
            // 若还未准备好则先转移当前的名称
            if(currentOwner != address(this)) {
                ens.setSubnodeOwner(ADDR_REVERSE_NODE, label, this);
                currentOwner = address(this);
            }
            ens.setResolver(node, resolver);
        }

        // 若所有者不一致则更新所有者
        if(currentOwner != owner) {
            ens.setSubnodeOwner(ADDR_REVERSE_NODE, label, owner);
        }

        return node;
    }

    /**
     * @dev 设置调用者的反向ENS记录的名称,如有需要则会先更新解析器为默认的反向解析器
     * @param name, 对应账户需要设置的名称
     * @return ENS 反向记录的哈希值
     */
    function setName(string name) returns (bytes32 node) {
        node = claimWithResolver(this, defaultResolver);
        defaultResolver.setName(node, name);
        return node;
    }

    /**
     * @dev 获取给定账户的反向记录的节点哈希值
     * @param addr 哈希过的地址
     * @return ENS 节点的哈希
     */
    function node(address addr) constant returns (bytes32 ret) {
        return sha3(ADDR_REVERSE_NODE, sha3HexAddress(addr));
    }

    /**
     * @dev 一个优化计算以太坊地址的小写的十六进制表示的sha3值
     * @param addr, 地址的哈希值
     * @return 对应输入地址的小写的十六进制编码的SHA3哈希值
     */
    function sha3HexAddress(address addr) private returns (bytes32 ret) {
        addr; ret; //对于未使用的变量不要发出警告
        assembly {
            let lookup := 0x3031323334353637383961626364656600000000000000000000000000000000
            let i := 40
        loop:
            i := sub(i, 1)
            mstore8(i, byte(and(addr, 0xf), lookup))
            addr := div(addr, 0x10)
            i := sub(i, 1)
            mstore8(i, byte(and(addr, 0xf), lookup))
            addr := div(addr, 0x10)
            jumpi(loop, i)
            ret := sha3(0, 40)
        }
    }
}
```

# 版权

Copyright and related rights waived via CC0.
